1 CLR


2 REM AUTHOR LUCA UBURTI, EMAIL:LUCA.UBURTI AT GMAIL.COM
3 REM DROP ME A NOTE IF YOU FOUND THIS FUNNY, AMUSING OR DARE I SAY USEFUL :)
4 REM THANKS!
5 REM 20 AUG 2020


100 REM MINEFIELD BLOCK VALUES 
101 REM 00: HIDDEN, 0 BOMBS NEAR
102 REM 01: HIDDEN, 1 BOMB NEAR
103 REM 02: HIDDEN, 2 BOMBS NEAR
104 REM 03: HIDDEN, 3 BOMBS  NEAR
105 REM 0X: ETC...
106 REM 10: VISIBLE, 0 BOMBS NEAR
107 REM 11: VISIBLE, 1 BOMB NEAR
108 REM 12: VISIBLE, 2 BOMBS NEAR
109 REM 13: VISIBLE, 3 BOMBS NEAR
110 REM 1X: ETC...
111 REM 99: HIDDEN, BOMB
112 REM 100: VISIBLE, BOMB! I GUESS WE'RE DEAD :(

115 IR%=0:IC%=0:REM INPUT COORDINATES
116 WN=0:REM WIN CONDITION

200 GOSUB 39000:REM INTRO SCREEN


500 GOSUB 40000: REM GENERATE MINEFIELD AND ADD A REASONABLE NUMBER OF BOMBS
510 GOSUB 41000: REM CALCULATE PROXIMITY VALUES
520 PRINT "":PRINT "GOOD LUCK!"
530 GOSUB 50000


1000 REM GOSUB 50100: REM DISPLAY BOARD - EVERYTHING VISIBILE
1001 REM TT=1:GOSUB 50000:REM DELAY
1005 GOSUB 60000:REM CHECK WIN CONDITION
1006 IF WN=1 THEN GOTO 10000
1010 GOSUB 53000: REM DISPLAY BOARD - NORMAL VISIBILITY
1020 GOSUB 54000: REM CHECK INPUTS
1030 IF MF%(IR%,IC%)=99 THEN GOSUB 55000:GOTO 10000:REM GAME OVER!
1040 IF MF%(IR%,IC%)>0 AND MF%(IR%,IC%)<10 THEN GOSUB 56000:GOTO 1000:REM NEAR
1050 IF MF%(IR%,IC%)=0 THEN GOSUB 57000:GOTO 1000:REM EMPTY



10000 REM END GAME
10010 PRINT "DO YOU WANT TO PLAY AGAIN? (Y/N)"
10015 AG$=""
10020 GET AG$:IF AG$="" THEN GOTO 10020
10025 IF AG$="Y" THEN GOTO 1
10030 PRINT "":PRINT "OK, THANKS FOR PLAYING, HAVE A NICE DAY!"
10050 TT=2:GOSUB 50000:REM DELAY
10060 END


39000 REM INTRO SCREEN
39010 PRINT CHR$(147):REM CLEAR SCREEN
39020 POKE 53280,0:POKE 53281,0:REM SET BORDER AND BACKGROUND BLACK
39110 INPUT "ENTER FIELD SIZE (5-10)"; FS%
39120 IF FS% < 5 OR FS% > 10 THEN PRINT "VALUE NOT PERMITTED":GOTO 10
39130 DIM MF%(FS%,FS%):REM DESC. OF BLOCK VALUES SEE LINE 100
39140 PRINT "FIELD WILL BE";
39150 PRINT FS%;:PRINT"X";:PRINT FS%:PRINT ""
39160 RETURN


40000 REM LET'S PUT BOMBS IN THE MINEFIELD!
40010 PRINT "HIDING THE BOMBS.";
40020 BD=0:REM BOMBS DEPLOYED
40025 X=RND(-TI):REM INIT RANDOM
40030 FOR R=0 TO FS%-1
40040 FOR C=0 TO FS%-1
40050 PB% = INT(RND(1)*100)+1: REM PROBABILITY TO INSERT A BOMB HERE
40060 IF PB%<4 AND MF%(R,C)<>99 THEN MF%(R,C)=99:BD=BD+1:PRINT ".";:REM NEW BOMB 
40070 NEXT C
40080 NEXT R
40090 PRINT ".";
40100 IF BD <FS%-2 GOTO 40030:REM TOO FEW BOMBS DEPLOYED, ONE MORE ROUND!
40110 PRINT"DONE":PRINT ""
40130 RETURN

41000 REM CALCULATE PROXIMITY VALUES
41005 PRINT "PREPARING GRID, PLEASE WAIT.";
41010 FOR R=0 TO FS%-1
41020 FOR C=0 TO FS%-1
41025 IF MF%(R,C)=99 THEN GOTO 41400:REM BOMB HERE, SKIP
41030 PV=0
41050 GOSUB 42000:REM CHECK CELLS NEARBY
41060 GOSUB 42100:
41070 GOSUB 42200
41080 GOSUB 42300
41090 GOSUB 42400
41100 GOSUB 42500
41110 GOSUB 42600
41120 GOSUB 42700
41130 IF PV > 0 THEN MF%(R,C)=PV:PRINT ".";
41400 NEXT C
41410 NEXT R
41420 PRINT "DONE"
41430 RETURN

42000 REM CHECK UPPER LEFT FOR BOMBS
42010 IF R-1<0 OR C-1<0 THEN RETURN:REM OUT OF BOUNDS
42030 IF MF%(R-1,C-1)=99 THEN PV=PV+1
42040 RETURN
42100 REM CHECK UP
42110 IF R-1<0 THEN RETURN:REM OUT OF BOUNDS
42130 IF MF%(R-1,C)=99 THEN PV=PV+1
42140 RETURN
42200 REM CHECK UPPER RIGHT
42210 IF R-1<0 OR C+1>FS%-1 THEN RETURN:REM OUT OF BOUNDS
42230 IF MF%(R-1,C+1)=99 THEN PV=PV+1
42240 RETURN
42300 REM CHECK LEFT
42310 IF C-1<0 THEN RETURN:REM OUT OF BOUNDS
42330 IF MF%(R,C-1)=99 THEN PV=PV+1
42340 RETURN
42400 REM CHECK RIGHT
42410 IF C+1>FS%-1 THEN RETURN:REM OUT OF BOUNDS
42430 IF MF%(R,C+1)=99 THEN PV=PV+1
42440 RETURN
42500 REM CHECK LOWER LEFT
42510 IF R+1 > FS%-1 OR C-1<0 THEN RETURN:REM OUT OF BOUNDS
42530 IF MF%(R+1,C-1)=99 THEN PV=PV+1
42540 RETURN
42600 REM CHECK DOWN
42610 IF R+1 > FS%-1 THEN RETURN:REM OUT OF BOUNDS
42630 IF MF%(R+1,C)=99 THEN PV=PV+1
42640 RETURN
42700 REM CHECK LOWER RIGHT
42710 IF R+1 > FS%-1 OR C+1>FS%-1 THEN RETURN:REM OUT OF BOUNDS
42730 IF MF%(R+1,C+1)=99 THEN PV=PV+1
42740 RETURN


50000 REM DELAY "TT" SECONDS
50010 FOR I=0 TO TT
50020 TI$="000000"
50030 WAIT 162,64
50040 NEXT
50050 RETURN

50100 REM DISPLAY BOARD - EVERYTHING VISIBLE 
50110 PRINT CHR$(147):REM CLEAR SCREEN
50120 FOR R=0 TO FS%-1
50130 FOR C=0 TO FS%-1
50140 IF R=0 AND C=0 THEN GOSUB 51000:REM DISPLAY COLUMNS NUMBERS
50150 IF R>0 AND C=0 THEN GOSUB 52000:REM DISPLAY ROWS NUMBERS
50155 REM TRIM SPACES AND VISIBILITY DIGIT
50156 IF MF%(R,C)>=10 THEN VL$=MID$(STR$(MF%(R,C)),3,1)
50157 IF MF%(R,C)<10 THEN VL$=MID$(STR$(MF%(R,C)),2,1)

50160 IF MF%(R,C)=10 OR MF%(R,C)=0 THEN PRINT "";
50165 IF MF%(R,C)=11 OR MF%(R,C)=1 THEN PRINT "";
50170 IF MF%(R,C)=12 OR MF%(R,C)=2 THEN PRINT "";
50175 IF MF%(R,C)>12 AND MF%(R,C)<99 THEN PRINT "";
50180 IF MF%(R,C)>2 AND MF%(R,C)<9 THEN PRINT "";
50185 IF MF%(R,C)<99 THEN PRINT VL$;:PRINT" ";

50460 REM IF MF%(R,C)<99 THEN PRINT VL$;:PRINT" ";:REM STANDARD CELL
50465 REM IF MF%(R,C)<99 THEN PRINT MF%(R,C);:REM NO FORMATTING
50470 IF MF%(R,C)=99 THEN PRINT "B ";:REM BOMB CELL
50480 IF MF%(R,C)=100 THEN PRINT "B ";:REM A BOMB JUST EXPLODED HERE
50500 IF C=FS%-1 THEN PRINT"":PRINT"":REM END OF ROW
50510 NEXT C
50520 NEXT R
50530 RETURN

51000 REM DISPLAY COLUMNS NUMBERS
51005 PRINT "    ";
51010 FOR I=0 TO FS%-1
51020 A$=STR$(I+1)
51050 PRINT A$;
51060 NEXT
51070 PRINT ""
51075 PRINT "    ";
51080 FOR I=0 TO FS%-1
51100 PRINT " .";
51110 NEXT
51170 PRINT"":PRINT ""
51180 PRINT "  1 -";: REM FIRST ROW NUMBER, HAS TO BE WRITTEN HERE...
51190 RETURN

52000 REM DISPLAY ROWS NUMBERS
52005 IF R+1 < 10 THEN PRINT" ";: PRINT R+1;:PRINT"-";:RETURN
52020 PRINT R+1;:PRINT"-";:RETURN

53000 REM DISPLAY BOARD - NORMAL VISIBILIY, NO CHEATING
53010 PRINT CHR$(147):REM CLEAR SCREEN
53020 FOR R=0 TO FS%-1
53030 FOR C=0 TO FS%-1
53040 IF R=0 AND C=0 THEN GOSUB 51000:REM DISPLAY COLUMNS NUMBERS
53050 IF R>0 AND C=0 THEN GOSUB 52000:REM DISPLAY ROWS NUMBERS
53060 VL$=MID$(STR$(MF%(R,C)),3,1):REM TRIM SPACES AND VISIBILITY DIGIT
53080 IF MF%(R,C)<10 OR MF%(R,C)=99 THEN PRINT "? ";:REM HIDDEN CELLS
53085 IF MF%(R,C)=10 THEN PRINT "";
53090 IF MF%(R,C)=11 THEN PRINT "";
53095 IF MF%(R,C)=12 THEN PRINT "";
53100 IF MF%(R,C)>12 AND MF%(R,C)<99 THEN PRINT "";
53105 IF MF%(R,C)>=10 AND MF%(R,C)<99 THEN PRINT VL$;:PRINT" ";
53110 IF C=FS%-1 THEN PRINT"":PRINT"":REM END OF ROW
53120 NEXT C
53130 NEXT R
53140 RETURN

54000 REM CHECK INPUTS
54010 INPUT "INSERT ROW,COLUMN: ";IR%,IC%
54020 EE%=0:REM ERROR BOOL
54030 IF IR%<1 OR IR%>FS% OR IC%<1 OR IC>FS% THEN EE%=1
54035 IR%=IR%-1:IC%=IC%-1:REM CONVERT INPUTS TO ARRAY COORDINATES
54040 IF EE%<>0 THEN PRINT "INVALID CELL":GOTO 54010
54050 IF MF%(IR%,IC%)>=10 AND MF%(IR%,IC%)<20 THEN ? "ALREADY CHOSEN":GOTO 54010
54200 RETURN

55000 REM STEPPED ON A BOMB! GAME OVER
55010 PRINT "!!!KABOOM!!!"
55020 TT=2:GOSUB 50000:REM DELAY
55030 MF%(IR%,IC%)=100
55040 GOSUB 50100:REM DISPLAY BOARD - EVERYTHING VISIBLE 
55050 RETURN

56000 REM CELL NEAR BOMB
56010 MF%(IR%,IC%)=MF%(IR%,IC%)+10:REM MAKE CELL VISIBLE
56020 RETURN

57000 REM EMPTY CELL SELECTED, UNCOVER NEARBY CELLS
57005 PRINT "SAFE CELL. UNCONVERING CELLS NEARBY";
57010 MF%(IR%,IC%)=MF%(IR%,IC%)+10:REM MAKE THIS CELL VISIBLE
57015 DD=0:REM DIRTY BIT TO CHECK IF WE NEED ANOTHER PASS
57020 FOR R=0 TO FS%-1
57030 FOR C=0 TO FS%-1
57040 VC=0:REM BOOL TO TELL IF A CELL MUST BE VISIBLE
57045 IF MF%(R,C)<10 THEN GOSUB 57500:REM CHECK FOR VISIBILE EMPTY CELL NEARBY
57060 IF VC=1 THEN GOSUB 59000:REM NEARBY CELLS VISIBLE, SO THIS ONE TOO
57070 NEXT C
57080 NEXT R
57090 IF DD=1 THEN PRINT ".";:GOTO 57015:REM NEW CELLS VISIBLE, START OVER
57100 REM PRINT "DONE UNCOVERING"
57110 RETURN

57500 REM DRIVER BLOCK TO CHECK NEARBY CELLS
57510 GOSUB 58000
57520 IF VC=1 THEN RETURN
57530 GOSUB 58100
57540 IF VC=1 THEN RETURN
57550 GOSUB 58200
57560 IF VC=1 THEN RETURN
57570 GOSUB 58300
57580 IF VC=1 THEN RETURN
57590 GOSUB 58400
57600 IF VC=1 THEN RETURN
57610 GOSUB 58500
57620 IF VC=1 THEN RETURN
57630 GOSUB 58600
57640 IF VC=1 THEN RETURN
57650 GOSUB 58700
57660 RETURN


58000 REM CHECK UPPER LEFT FOR A VISIBILE EMPTY CELL
58010 IF R-1<0 OR C-1<0 THEN RETURN:REM OUT OF BOUNDS, NOTHING TO CHECK
58030 IF MF%(R-1,C-1)=10 THEN VC=1
58040 RETURN
58100 REM CHECK UP
58110 IF R-1<0 THEN RETURN:REM OUT OF BOUNDS, NOTHING TO CHECK
58130 IF MF%(R-1,C)=10 THEN VC=1
58140 RETURN
58200 REM CHECK UPPER RIGHT
58210 IF R-1<0 OR C+1>FS%-1 THEN RETURN:REM OUT OF BOUNDS, NOTHING TO CHECK
58230 IF MF%(R-1,C+1)=10 THEN VC=1
58240 RETURN
58300 REM CHECK LEFT
58310 IF C-1<0 THEN RETURN:REM OUT OF BOUNDS, NOTHING TO CHECK
58330 IF MF%(R,C-1)=10 THEN VC=1
58340 RETURN
58400 REM CHECK RIGHT
58410 IF C+1>FS%-1 THEN RETURN:REM OUT OF BOUNDS, NOTHING TO CHECK
58430 IF MF%(R,C+1)=10 THEN VC=1
58440 RETURN
58500 REM CHECK LOWER LEFT
58510 IF R+1 > FS%-1 OR C-1<0 THEN RETURN:REM OUT OF BOUNDS, NOTHING TO CHECK
58530 IF MF%(R+1,C-1)=10 THEN VC=1
58540 RETURN
58600 REM CHECK DOWN
58610 IF R+1 > FS%-1 THEN RETURN:REM OUT OF BOUNDS, NOTHING TO CHECK
58620 IF MF%(R+1,C)=10 THEN VC=1
58630 RETURN
58700 REM CHECK LOWER RIGHT
58710 IF R+1 > FS%-1 OR C+1>FS%-1 THEN RETURN:REM OUT OF BOUNDS, NOTHING TO CHECK
58720 IF MF%(R+1,C+1)=10 THEN VC=1
58730 RETURN


59000 REM SET VISIBLE
59010 MF%(R,C)=MF%(R,C)+10
59020 REM PRINT "SET VISIBLE ";:PRINT R,C
59030 DD=1:REM SET DIRTY BIT, ANOTHER PASS WILL BE NEEDED
59040 RETURN

60000 REM CHECK WIN CONDITION
60010 FOR R=0 TO FS%-1
60020 FOR C=0 TO FS%-1
60040 IF MF%(R,C)<10 THEN WN=0:GOSUB 50000:RETURN:REM HIDDEN CELLS, NOT WON YET
60045 
60060 NEXT C
60070 NEXT R
60080 WN=1:REM IF WE GET THIS FAR WE WON!
60090 PRINT "!!!!!! YOU WON !!!!!!"
60100 GOSUB 50000:GOSUB 50000:REM DELAY
60110 GOSUB 50100:REM DISPLAY BOARD - EVERYTHING VISIBLE 
60120 GOSUB 50000:REM DELAY
60130 
60140 RETURN



